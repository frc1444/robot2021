buildscript {
    ext {
        kotlinVersion = '1.4.21' // https://kotlinlang.org/releases.html
        wpiLibVersion = "2021.1.1-beta-4" // https://github.com/wpilibsuite/allwpilib/releases // TODO change
        junit5Version = "5.7.0" // https://junit.org/junit5/
        gdxVersion = '1.9.12' // https://github.com/libgdx/libgdx/releases
//        roboSimVersion = "2de59f2ec1"
        roboSimVersion = "3e3c3d868ee1a9315bcad8a8a299b3c20d17f257".substring(0, 10)
        abstractControllerLibVersion = "v2.5.0"
        abstractDashboardVersion = "e635c7080d"
        jacksonVersion = "2.12.0" // https://github.com/FasterXML/jackson-databind/releases
    }
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        jcenter()
        google()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

subprojects {
    apply plugin: "kotlin"
    ext { // some of the variables in here may only be specific to a single module
        appName = "robot2021-sim-app"
        gdxVersion = "1.9.10"
    }
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        google()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        maven { url "https://jitpack.io" }
        maven { url "https://frcmaven.wpi.edu/artifactory/release" }
    }
    dependencies {
        api "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
        testImplementation "org.junit.jupiter:junit-jupiter-engine:$junit5Version"
        testImplementation "org.junit.jupiter:junit-jupiter-api:$junit5Version"
    }
    test {
        useJUnitPlatform()
    }
    compileKotlin {
        kotlinOptions {
            jvmTarget = "11"
        }
    }
}
project(":core"){
    apply plugin: "java-library"
}
project(":gdx"){
    apply plugin: "java-library"
    version = "1.0"
    dependencies {
        api project(":core") // if we change this to 'implementation', the desktop:dist jar produced stops working...
    }
}
project(":wpi"){
    apply plugin: "java"
    dependencies {
        api project(":core")
    }
}

project(":desktop") {
    apply plugin: "java-library"

    dependencies {
        api project(":gdx")
    }
}

wrapper {
    gradleVersion = '6.7.1' // https://gradle.org/releases/
    distributionType = Wrapper.DistributionType.ALL
}
